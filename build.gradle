import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: '1.3.72'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.20.0'
    id 'de.marcphilipp.nexus-publish' version '0.2.0'
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.dokka'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

nexusStaging {
    packageGroup = "com.github.shynixn"
    username = project.hasProperty("ossrhUsername") ? project.findProperty("ossrhUsername") : System.getenv('SONATYPE_USERNAME')
    password = project.hasProperty("ossrhPassword") ? project.findProperty("ossrhPassword") : System.getenv('SONATYPE_PASSWORD')
    delayBetweenRetriesInMillis = 10000
    numberOfRetries = 100
}

allprojects {
    apply plugin: 'jacoco'
}

subprojects {
    group 'com.github.shynixn.petblocks'
    version '8.23.0'

    sourceCompatibility = 1.8

    apply plugin: 'kotlin-platform-jvm'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'java-library'
    apply plugin: 'de.marcphilipp.nexus-publish'
    apply plugin: 'org.jetbrains.dokka'

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    test {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
        failFast = true

        testLogging {
            events(TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.STARTED)
            displayGranularity = 0
            showExceptions = true
            showCauses = true
            showStackTraces = true
            exceptionFormat = TestExceptionFormat.FULL
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"
    }

    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
        from javadoc
        classifier = 'javadoc'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'PetBlocks'
                    description = 'PetBlocks is a spigot and also a sponge plugin to use blocks and custom heads as pets in Minecraft.'
                    url = 'https://github.com/Shynixn/PetBlocks'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Shynixn'
                            url = 'https://github.com/Shynixn'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/Shynixn/PetBlocks.git'
                        developerConnection = 'scm:git:ssh://github.com:Shynixn/PetBlocks.git'
                        url = 'http://github.com/Shynixn/PetBlocks.git/tree/master'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = hasProperty("ossrhUsername") ? findProperty("ossrhUsername") : System.getenv('SONATYPE_USERNAME')
                    password = hasProperty("ossrhPassword") ? findProperty("ossrhPassword") : System.getenv('SONATYPE_PASSWORD')
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        testCompile 'org.jetbrains.kotlin:kotlin-test'
        testCompile 'org.jetbrains.kotlin:kotlin-test-junit'
        testCompile 'org.mockito:mockito-core:3.6.28'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'

        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (project.findProperty("signing.keyId") == null) {
        ext."signing.keyId" = System.getenv('SIGNING_KEYID')
        ext."signing.password" = System.getenv('SIGNING_KEYPASSWORD')
        ext."signing.secretKeyRingFile" = '/home/travis/build/Shynixn/PetBlocks/travis_secret_key'
    }
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}


/**
 * Generates javadocs.
 */
task generateJavaDocPages(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "docs/apidocs"

    configuration {
        sourceRoot {
            path = "petblocks-api/src/main/kotlin"
        }
        sourceRoot {
            path = "petblocks-core/src/main/kotlin"
        }
        sourceRoot {
            path = "petblocks-bukkit-api/src/main/kotlin"
        }
        sourceRoot {
            path = "petblocks-bukkit-plugin/src/main/kotlin"
        }
        sourceRoot {
            path = "petblocks-sponge-api/src/main/java"
        }
        sourceRoot {
            path = "petblocks-sponge-plugin/src/main/java"
        }
    }
}

/**
 * Downloads additional dependencies which are not available in
 * maven central or other repositories.
 */
task downloadDependencies(type: Exec) {
    def build = new File('build')

    if (!build.exists()) {
        build.mkdir()
    }

    def folder = new File(build, 'buildtools')

    if (!folder.exists()) {
        folder.mkdir()
    }

    def file = new File(folder, "BuildTools.jar")

    if (!file.exists()) {
        new URL('https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar').withInputStream { i -> file.withOutputStream { it << i } }
    }

    def hdbFile = new File(folder, "HeadDatabaseAPI.jar")

    if (!hdbFile.exists()) {
        def lookConnection = new URL('http://www.mediafire.com/file/x0pl7biy9picco1/HeadDatabaseAPI.jar/file').openConnection()
        lookConnection.setRequestProperty("User-Agent", "Mozilla/5.0 (Macintosh; U; Intel Mac OS X 10.4; en-US; rv:1.9.2.2) Gecko/20100316 Firefox/3.6.2");
        def text = lookConnection.getInputStream().text
        def startIndex = text.indexOf("aria-label=\"Download file\"") + 40
        def endIndex = text.indexOf(">\n" + "                        Download (")
        def downloadLink = text.substring(startIndex as int, endIndex as int).replace("href=\"", "").trim()

        def connection = new URL(downloadLink).openConnection()
        connection.setRequestProperty("User-Agent", "Mozilla/5.0 (Macintosh; U; Intel Mac OS X 10.4; en-US; rv:1.9.2.2) Gecko/20100316 Firefox/3.6.2");
        def out = new BufferedOutputStream(new FileOutputStream(hdbFile))
        out << connection.inputStream
        out.close()
    }

    def text = "cd build/buildtools"
    text += " && mvn install:install-file -Dfile=HeadDatabaseAPI.jar -DgroupId=me.arcaniax.hdb -DartifactId=HeadDatabaseAPI -Dversion=1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.8.8"
    text += " && mvn install:install-file -Dfile=spigot-1.8.8.jar -DgroupId=org.spigotmc -DartifactId=spigot18R3 -Dversion=1.8.8-R3.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.9.4"
    text += " && mvn install:install-file -Dfile=spigot-1.9.4.jar -DgroupId=org.spigotmc -DartifactId=spigot19R2 -Dversion=1.9.4-R2.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.10"
    text += " && mvn install:install-file -Dfile=spigot-1.10.2.jar -DgroupId=org.spigotmc -DartifactId=spigot110R1 -Dversion=1.10.2-R1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.11"
    text += " && mvn install:install-file -Dfile=spigot-1.11.jar -DgroupId=org.spigotmc -DartifactId=spigot111R1 -Dversion=1.11.0-R1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.12"
    text += " && mvn install:install-file -Dfile=spigot-1.12.jar -DgroupId=org.spigotmc -DartifactId=spigot112R1 -Dversion=1.12.0-R1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.13.2"
    text += " && mvn install:install-file -Dfile=spigot-1.13.2.jar -DgroupId=org.spigotmc -DartifactId=spigot113R2 -Dversion=1.13.2-R2.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.14.4"
    text += " && mvn install:install-file -Dfile=spigot-1.14.4.jar -DgroupId=org.spigotmc -DartifactId=spigot114R1 -Dversion=1.14.4-R1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.15"
    text += " && mvn install:install-file -Dfile=spigot-1.15.jar -DgroupId=org.spigotmc -DartifactId=spigot115R1 -Dversion=1.15.0-R1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.16.1"
    text += " && mvn install:install-file -Dfile=spigot-1.16.1.jar -DgroupId=org.spigotmc -DartifactId=spigot116R1 -Dversion=1.16.0-R1.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.16.2"
    text += " && mvn install:install-file -Dfile=spigot-1.16.2.jar -DgroupId=org.spigotmc -DartifactId=spigot116R2 -Dversion=1.16.2-R2.0 -Dpackaging=jar"
    text += " && java -jar BuildTools.jar --rev 1.16.4"
    text += " && mvn install:install-file -Dfile=spigot-1.16.4.jar -DgroupId=org.spigotmc -DartifactId=spigot116R3 -Dversion=1.16.4-R3.0 -Dpackaging=jar"

    standardOutput = new ByteArrayOutputStream()

    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', text
    } else {
        commandLine 'sh', '-c', text
    }
}
