buildscript {
    repositories {
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.vanillagradle.server' version '2.2-6'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

minecraft {
    version = '1.12'
    mappings = 'snapshot_20180323'
}

apply plugin: 'jacoco'

shadowJar {
    from sourceSets.main.resources.srcDirs

    archiveName = "$baseName-$version.$extension"

    // Change the output folder of the plugin.
    // destinationDir = new File('D:\\Benutzer\\Temp\\SpongePowered\\Sponge-2825-7.1.6\\mods')

    relocate 'kotlin', 'com.github.shynixn.petblocks.lib.kotlin'
    relocate 'org.intellij', 'com.github.shynixn.petblocks.lib.org.intellij'
    relocate 'org.jetbrains', 'com.github.shynixn.petblocks.lib.org.jetbrains'
    relocate 'javax.inject', 'com.github.shynixn.petblocks.lib.javax.inject'
    relocate 'org.aopalliance', 'com.github.shynixn.petblocks.lib.org.aopalliance'
    relocate("com.fasterxml", "com.github.shynixn.petblocks.lib.com.fasterxml")
    relocate("com.github.shynixn.mccoroutine", "com.github.shynixn.petblocks.lib.com.github.shynixn.mccoroutine")

    exclude "dummyThing"
    exclude("DebugProbesKt.bin")
    exclude("module-info.class")
}

jar {
    destinationDir = shadowJar.destinationDir

    dependsOn shadowJar
    enabled = false
    finalizedBy reobfJar
}

shadowJar.finalizedBy reobfJar

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

task dockerJar(type: Exec, dependsOn: shadowJar) {
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', "docker cp build/libs/. petblocks-sponge-1.12.2:/minecraft/mods"
    } else {
        commandLine 'sh', '-c', "docker cp build/libs/. petblocks-sponge-1.12.2:/minecraft/mods"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    implementation project(path: ':petblocks-core', configuration: 'default')
    implementation project(path: ':petblocks-api', configuration: 'default')
    implementation project(path: ':petblocks-sponge-api', configuration: 'default')

    compileOnly 'com.zaxxer:HikariCP:3.4.3'
    compileOnly 'org.spongepowered:spongeapi:7.1.0'

    testCompile 'org.xerial:sqlite-jdbc:3.31.1'
    testCompile 'org.spongepowered:spongeapi:7.1.0'
    testCompile 'ch.vorburger.mariaDB4j:mariaDB4j:2.4.0'
    testCompile 'mysql:mysql-connector-java:5.1.24'

    testRuntime 'com.zaxxer:HikariCP:3.2.0'
    testRuntime 'org.slf4j:slf4j-jdk14:1.7.30'
}
